&НаКлиенте
Перем WshShell;

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПроверитьСоздатьКаталог(Знач КаталогДляПроверки)
	
	ВыбФайл = Новый Файл(КаталогДляПроверки);		
	Если Не ВыбФайл.Существует() Тогда
		СоздатьКаталог(КаталогДляПроверки);
	КонецЕсли;
	
КонецПроцедуры	

&НаКлиенте
Процедура vsc_СоздатьФайлОбработкиМенеджераОбмена(Команда)
	
	Настройки = ПрочитатьНастройки(Объект.Конвертация);
	
	Если Не ЗначениеЗаполнено(Настройки.КаталогДляГотовыхФайлов) Тогда
		// Усли настройки не указаны, открываем форму редактирования настроек.
		ПараметрыФормыНастроек = Новый Структура;
		ПараметрыФормыНастроек.Вставить("Конвертация",Объект.Конвертация);
		ОткрытьФорму("Обработка.vsc_НастройкаПравилСозданияМенеджераОбмена.Форма.Форма",ПараметрыФормыНастроек);		
		
		Возврат;
		
	КонецЕсли;	
	
	РежимОтладки = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Настройки, "РежимОтладки", Ложь);
	
	// Определим переменные окружения.
	КаталогДляГотовыхФайлов        = Настройки.КаталогДляГотовыхФайлов;
	КаталогДляГотовыхФайлов        = ДополнитьСлешВПуть(КаталогДляГотовыхФайлов);
	
	ВременныйКаталогБазы           = ОбщегоНазначенияКлиентСервер.КаталогФайловойИнформационнойБазы();
		
	ВерсияПлатформыДляГенерацииEPF = Настройки.ВерсияПлатформыДляГенерацииEPF;
	Если Не ЗначениеЗаполнено(ВерсияПлатформыДляГенерацииEPF) Тогда	
		ВерсияПлатформыДляГенерацииEPF = "C:\Program Files (x86)\1cv8\";
	КонецЕсли;
	
	ВерсияПлатформыДляГенерацииEPF = ДополнитьСлешВПуть(ВерсияПлатформыДляГенерацииEPF);
	
	ВерсияПлатформыДляГенерацииEPF =  ВерсияПлатформыДляГенерацииEPF + ВерсияПриложения() + "\bin\";
	
	// Формируем имя временного католога с исходниками обработки.
	ОписаниеТиповСтрока 		= Новый ОписаниеТипов("Строка");
	ИдентификаторКонвертации 	= ОписаниеТиповСтрока.ПривестиЗначение(Объект.Конвертация.УникальныйИдентификатор());
	КаталогДляВременныхФайлов	= КаталогДляГотовыхФайлов + ИдентификаторКонвертации + "\";
	
	// Проверям если каталог не существует, то создаем его.	
	ПроверитьСоздатьКаталог(КаталогДляВременныхФайлов);
	ПроверитьСоздатьКаталог(КаталогДляГотовыхФайлов);
	
	// Создаем внешнюю обработку в формате xml.
	// Сохраняем xml-макет внешней обработки.
	КорневойФайлОбработки = КаталогДляВременныхФайлов + "Обработка.xml"; 
	ПолучитьОбщийМакетНаСервере().Записать(КорневойФайлОбработки);
	
	// Создаем структуру внешней обработки.
	КаталогМодуляОбработки = КаталогДляВременныхФайлов + "Обработка\Ext\";
	СоздатьКаталог(КаталогМодуляОбработки);
	
	// Инициализируем модуль внешней обработки.
	ТекДок = Новый ТекстовыйДокумент;
	ТекДок.ДобавитьСтроку(СохранитьМодульСервер().ПолучитьТекст());
	ТекДок.Записать(КаталогМодуляОбработки + "ObjectModule.bsl"); 
	
	ИмяВнешнейОбработки = ИмяВнешнейОбработки();
	ИмяВременногоEPF    = КаталогДляВременныхФайлов + ИмяВнешнейОбработки;
	ИмяГотовогоEPF      = КаталогДляГотовыхФайлов + ИмяВнешнейОбработки;
	
	ЭтоLinux = ЭтоLinuxСервер();	
	Если ЭтоLinux Тогда
		СтрокаЗапуска1С = "" + ДополнитьСлешВПуть(ВерсияПлатформыДляГенерацииEPF) + "1cv8 DESIGNER /F""" 
		+ ВременныйКаталогБазы + """ /LoadExternalDataProcessorOrReportFromFiles """ + КорневойФайлОбработки 
		+ """ """ + ИмяВременногоEPF + """";
	Иначе
		СтрокаЗапуска1С = """" + ДополнитьСлешВПуть(ВерсияПлатформыДляГенерацииEPF) + "1cv8"" DESIGNER /F """ 
		+ ВременныйКаталогБазы + """ /LoadExternalDataProcessorOrReportFromFiles """ + КорневойФайлОбработки 
		+ """ """ + ИмяВременногоEPF + """";
	КонецЕсли;
	
	Если РежимОтладки Тогда
		
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = "КаталогДляГотовыхФайлов: " + КаталогДляГотовыхФайлов ;
		Сообщение.Сообщить();		
		
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = "КаталогБазы: " + ВременныйКаталогБазы;
		Сообщение.Сообщить();		
		
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = "КаталогДляВременныхФайлов: " + КаталогДляВременныхФайлов;
		Сообщение.Сообщить();		
		
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = "СтрокаЗапуска1С: " + СтрокаЗапуска1С;
		Сообщение.Сообщить();		

	КонецЕсли;
	
	ОписаниеЗапуска = Запустить1С(СтрокаЗапуска1С, КаталогДляВременныхФайлов);
	
	// Копируем готовую обработку из каталога временных файлов.
	КопироватьФайл(ИмяВременногоEPF, ИмяГотовогоEPF);
	
	// Удалим каталог временных файлов.
	Если Не РежимОтладки Тогда
		УдалитьФайлы(КаталогДляВременныхФайлов);
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ПрочитатьНастройки(Знач Конвертация)
	Возврат Обработки.vsc_НастройкаПравилСозданияМенеджераОбмена.ПрочитатьНастройки(Конвертация);
КонецФункции

&НаСервере
Функция ИмяВнешнейОбработки()
	Возврат "МенеджерОбмена" + СокрЛП(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Конвертация, "Наименование")) + ".epf";	
КонецФункции	

// Выполняет запуск 1С (Конфигуратор, Предприятие) и возвращает структурус путями к файлам логов (ключ ИмяФайлаOUT и ИмяФайлаЛога)
// ВНИМАНИЕ: файлы логов нужно удалять самостоятельно.
//
// Параметры:
//   СтрокаЗапуска1С - Строка - строка запуска 1С
//
//  Возвращаемое значение:
//   Структура - структура с путями к файлам логов (ключ ИмяФайлаOUT и ИмяФайлаЛога)
//
&НаКлиенте
Функция Запустить1С(Знач СтрокаЗапуска1С, Знач КаталогДляВременныхФайлов)
	Рез = Новый Структура;
	
	ИмяФайлаЛога = ПолучитьПутьВременногоФайла(КаталогДляВременныхФайлов, "txt");
	ИмяФайлаOUT = ПолучитьПутьВременногоФайла(КаталогДляВременныхФайлов, "txt");
	
	Рез.Вставить("ИмяФайлаЛога", ИмяФайлаЛога);
	Рез.Вставить("ИмяФайлаOUT", ИмяФайлаOUT);
	
	КомандаСистемы = СтрокаЗапуска1С + " /out " + ИмяФайлаOUT + " > " + ИмяФайлаЛога;
	
	//Отладка(КомандаСистемы);
	ВыполнитьКомандуОСБезПоказаЧерногоОкна(КомандаСистемы, Истина, Ложь); //TODO код возврата получить, хотя бы от Конфигуратора??
	
	Возврат Рез;
	
КонецФункции

Функция ПолучитьПутьВременногоФайла(Знач КаталогДляВременныхФайлов, Знач Расширение = "")
	НовоеИмя = ПолучитьИмяВременногоФайла(Расширение);
	Файл = Новый Файл(НовоеИмя);
	НовоеИмя = КаталогДляВременныхФайлов + "/" + Файл.Имя;
	Возврат НовоеИмя;
КонецФункции

&НаКлиенте
Функция ВыполнитьКомандуОСБезПоказаЧерногоОкна(Знач ТекстКоманды, Знач ЖдатьОкончания = Истина, 
	Знач ИспользоватьКодировкуТекстаUTF8 = Истина) Экспорт
	
	Если ЖдатьОкончания = -1 Тогда
		ЖдатьОкончания = Истина;
	ИначеЕсли ЖдатьОкончания = 0 Тогда
		ЖдатьОкончания = Ложь;
	КонецЕсли;

	УдалятьФайл = Ложь;
	ИмяВременногоФайлаКоманды = ТекстКоманды;
	Если ИспользоватьКодировкуТекстаUTF8 Тогда
		
		ИмяВременногоФайлаКоманды = ПолучитьИмяВременногоФайла("bat");
		
		//эти строки нужны для записи файла без BOM
		ЗТ = Новый ЗаписьТекста(ИмяВременногоФайлаКоманды, "CESU-8", , Ложь);
		ЗТ.ЗаписатьСтроку("chcp 65001");
	
		ЗТ.ЗаписатьСтроку(ТекстКоманды);
		ЗТ.Закрыть();
		
		УдалятьФайл = Истина;		
	КонецЕсли;
	
	ИмяВременногоФайлаКоманды = "cmd /c """ + ИмяВременногоФайлаКоманды + """";

	//КонтекстЯдра.Отладка(ТекстКоманды);
	//КонтекстЯдра.Отладка(ИмяВременногоФайлаКоманды);
	WshShell = Неопределено;
	Если WshShell = Неопределено Тогда
		WshShell = ПолучитьWshShell();
	КонецЕсли;

	Рез = WshShell.Run(ИмяВременногоФайлаКоманды, 0, ?(ЖдатьОкончания, -1, 0));

	Если ЖдатьОкончания И УдалятьФайл Тогда
		//иначе удалять нельзя
		////////Если КонтекстЯдра.ЕстьПоддержкаАсинхронныхВызовов Тогда
		////////	// для скорости не удаляем временный файл, сервер потом удалит КонтекстЯдра.УдалитьФайлыКомандаСистемы(ИмяВременногоФайлаКоманды);
		////////Иначе
		////////	УдалитьФайлы(ИмяВременногоФайлаКоманды);
		////////КонецЕсли;	 
	КонецЕсли;	 

	Возврат Рез;
КонецФункции

// далее переменная WshShell будет закеширована, чтобы не создавать ComObject каждый раз
&НаКлиенте
Функция ПолучитьWshShell() Экспорт

	Если WshShell = Неопределено Тогда
		Попытка
			WshShell = Новый COMОбъект("WScript.Shell");
		Исключение
			ВызватьИсключение "Не удалось подключить COM объект <WScript.Shell>";
		КонецПопытки;
	КонецЕсли;

	Возврат WshShell;

КонецФункции

&НаКлиенте
Функция ДополнитьСлешВПуть(Знач Каталог) Экспорт
	ЭтоLinux = ЭтоLinuxСервер();
	
	Разделитель = "\";
	
	Если ПустаяСтрока(Каталог) Тогда
		Возврат Каталог;
	КонецЕсли;
	Если ЭтоLinux Тогда
		Разделитель = "/";
		Каталог = СтрЗаменить(Каталог, "\", "/");
	КонецЕсли;
	
	Если Прав(Каталог, 1) <> Разделитель Тогда
		Каталог = Каталог + Разделитель;
	КонецЕсли;
	Возврат Каталог;
КонецФункции

&НаСервере
Функция ПолучитьОбщийМакетНаСервере()
	  Возврат ПолучитьОбщийМакет("vsc_СтруктураВнешнейОбработки");
КонецФункции	

Функция ВерсияПриложения() Экспорт
	
	СистемнаяИнформация = Новый СистемнаяИнформация;
	Возврат СистемнаяИнформация.ВерсияПриложения;
	
КонецФункции

Функция ЭтоLinuxСервер() Экспорт
	
	СистемнаяИнформация = Новый СистемнаяИнформация;
	Возврат СистемнаяИнформация.ТипПлатформы = ТипПлатформы.Linux_x86 ИЛИ СистемнаяИнформация.ТипПлатформы = ТипПлатформы.Linux_x86_64;
	
КонецФункции

#КонецОбласти
